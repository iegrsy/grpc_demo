plugins {
    id 'java-library'
    id 'com.google.protobuf' version '0.8.5'
}

repositories {
    mavenCentral()
}

version '0.0.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def grpcVersion = '1.16.1'
def protobufVersion = '3.6.1'
def protocVersion = protobufVersion

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
}

def protoGenPath = "$projectDir/build/proto_gen"

protobuf {
    generatedFilesBaseDir = "${protoGenPath}"
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins { grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" } }
    generateProtoTasks { all()*.plugins { grpc {} } }
}

sourceSets {
    main {
        java {
            srcDirs "${protoGenPath}/main/grpc"
            srcDirs "${protoGenPath}/main/java"
        }
    }
}